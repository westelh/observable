apply plugin: 'cpp'
apply plugin: 'google-test'

model {
	flavors {
        passing
        failing
    }
    buildTypes {
        debug
        release
    }
	components {
        observer(NativeLibrarySpec) 
		observable(NativeLibrarySpec) {
            sources {
                cpp.lib library: "observer"
            }
        }
	}
	repositories {
        libs(PrebuiltLibraries) {
            googleTest {
                headers.srcDir "gtest/build/install/include"
                binaries.withType(StaticLibraryBinary) {
                    staticLibraryFile = file("gtest\\build\\install\\lib\\libgtest.a")
                }
            }
        }
    }
	binaries {
        all {
            if (toolChain in GccCompatibleToolChain) {
                cppCompiler.args "-std=c++14", "-Wall", "-Wextra"
                if (buildType == buildTypes.debug)
                    cppCompiler.args "-g3", "-O0"
                else if (buildType == buildTypes.release)
                    cppCompiler.args "-O2"
            }
        }
		withType(GoogleTestTestSuiteBinarySpec) {
            lib library: "googleTest", linkage: "static"

            if (flavor == flavors.failing) {
                cppCompiler.define "PLUS_BROKEN"
            }

            if (targetPlatform.operatingSystem.linux) {
                cppCompiler.args '-pthread'
                linker.args '-pthread'
            }
        }
	}
}
